# PF Library

# Author: Michail Petrushin (misha@design.ru)
# http://parser.ru/examples/date/

@CLASS
pfDtf

@auto[][tmp]
$sClassName[pfDtf]

# russian locale
$rr-locale[
  $.month[
    $.1[Января]
    $.2[Февраля]
    $.3[Марта]
    $.4[Апреля]
    $.5[Мая]
    $.6[Июня]
    $.7[Июля]
    $.8[Августа]
    $.9[Сентября]
    $.10[Октября]
    $.11[Ноября]
    $.12[Декабря]
  ]
  $.weekday[
    $.0[Воскресенья]
    $.1[Понедельника]
    $.2[Вторника]
    $.3[Среды]
    $.4[Четверга]
    $.5[Пятницы]
    $.6[Субботы]
    $.7[Воскресенья]
  ]
]

$rr-low-locale[
  $.month[
    $.1[января]
    $.2[февраля]
    $.3[марта]
    $.4[апреля]
    $.5[мая]
    $.6[июня]
    $.7[июля]
    $.8[августа]
    $.9[сентября]
    $.10[октября]
    $.11[ноября]
    $.12[декабря]
  ]
  $.weekday[
    $.0[воскресенья]
    $.1[понедельника]
    $.2[вторника]
    $.3[среды]
    $.4[четверга]
    $.5[пятницы]
    $.6[субботы]
    $.7[воскресенья]
  ]
]
$ri-locale[
  $.month[
    $.1[Январь]
    $.2[Февраль]
    $.3[Март]
    $.4[Апрель]
    $.5[Май]
    $.6[Июнь]
    $.7[Июль]
    $.8[Август]
    $.9[Сентябрь]
    $.10[Октябрь]
    $.11[Ноябрь]
    $.12[Декабрь]
  ]
  $.weekday[
    $.0[Воскресенье]
    $.1[Понедельник]
    $.2[Вторник]
    $.3[Среда]
    $.4[Четверг]
    $.5[Пятница]
    $.6[Суббота]
    $.7[Воскресенье]
  ]
]
$ri-low-locale[
  $.month[
    $.1[январь]
    $.2[февраль]
    $.3[март]
    $.4[апрель]
    $.5[май]
    $.6[июнь]
    $.7[июль]
    $.8[август]
    $.9[сентябрь]
    $.10[октябрь]
    $.11[ноябрь]
    $.12[декабрь]
  ]
  $.weekday[
    $.0[воскресенье]
    $.1[понедельник]
    $.2[вторник]
    $.3[среда]
    $.4[четверг]
    $.5[пятница]
    $.6[суббота]
    $.7[воскресенье]
  ]
]
$rs-locale[
  $.month[
    $.1[Янв]
    $.2[Фев]
    $.3[Мар]
    $.4[Апр]
    $.5[Май]
    $.6[Июн]
    $.7[Июл]
    $.8[Авг]
    $.9[Сен]
    $.10[Окт]
    $.11[Ноя]
    $.12[Дек]
  ]
  $.weekday[
    $.0[Вс]
    $.1[Пн]
    $.2[Вт]
    $.3[Ср]
    $.4[Чт]
    $.5[Пт]
    $.6[Сб]
    $.7[Вс]
  ]
]
$rs-low-locale[
  $.month[
    $.1[янв]
    $.2[фев]
    $.3[мар]
    $.4[апр]
    $.5[май]
    $.6[июн]
    $.7[июл]
    $.8[авг]
    $.9[сен]
    $.10[окт]
    $.11[ноя]
    $.12[дек]
  ]
  $.weekday[
    $.0[вс]
    $.1[пн]
    $.2[вт]
    $.3[ср]
    $.4[чт]
    $.5[пт]
    $.6[сб]
    $.7[вс]
  ]
]
# english locale
$es-locale[
  $.month[
    $.1[Jan]
    $.2[Feb]
    $.3[Mar]
    $.4[Apr]
    $.5[May]
    $.6[Jun]
    $.7[Jul]
    $.8[Aug]
    $.9[Sep]
    $.10[Oct]
    $.11[Nov]
    $.12[Dec]
  ]
  $.weekday[
    $.0[Sun]
    $.1[Mon]
    $.2[Tue]
    $.3[Wed]
    $.4[Thu]
    $.5[Fri]
    $.6[Sat]
    $.7[Sun]
  ]
]
$ei-locale[
  $.month[
    $.1[January]
    $.2[February]
    $.3[March]
    $.4[April]
    $.5[May]
    $.6[June]
    $.7[July]
    $.8[August]
    $.9[September]
    $.10[October]
    $.11[November]
    $.12[December]
  ]
  $.weekday[
    $.0[Sunday]
    $.1[Monday]
    $.2[Tuesday]
    $.3[Wednesday]
    $.4[Thursday]
    $.5[Friday]
    $.6[Saturday]
    $.7[Sunday]
  ]
]
$es-low-locale[
  $.month[
    $.1[jan]
    $.2[feb]
    $.3[mar]
    $.4[apr]
    $.5[may]
    $.6[jun]
    $.7[jul]
    $.8[aug]
    $.9[sep]
    $.10[oct]
    $.11[nov]
    $.12[dec]
  ]
  $.weekday[
    $.0[sun]
    $.1[mon]
    $.2[tue]
    $.3[wed]
    $.4[thu]
    $.5[fri]
    $.6[sat]
    $.7[sun]
  ]
]  
$ei-low-locale[
  $.month[
    $.1[january]
    $.2[february]
    $.3[march]
    $.4[april]
    $.5[may]
    $.6[june]
    $.7[july]
    $.8[august]
    $.9[september]
    $.10[october]
    $.11[november]
    $.12[december]
  ]
  $.weekday[
    $.0[sunday]
    $.1[monday]
    $.2[tuesday]
    $.3[wednesday]
    $.4[thursday]
    $.5[friday]
    $.6[saturday]
    $.7[sunday]
  ]
]
# ukrain locale
$us-locale[
  $.month[
    $.1[Сiч]
    $.2[Лют]
    $.3[Бер]
    $.4[Квi]
    $.5[Тра]
    $.6[Чер]
    $.7[Лип]
    $.8[Сер]
    $.9[Вер]
    $.10[Жов]
    $.11[Лис]
    $.12[Гру]
  ]
  $.weekday[
    $.0[Нед]
    $.1[Пон]
    $.2[Вiв]
    $.3[Сер]
    $.4[Чет]
    $.5[П'я]
    $.6[Суб]
    $.7[Нед]
  ]
]
$ui-locale[
  $.month[
    $.1[Сiчень]
    $.2[Лютий]
    $.3[Березень]
    $.4[Квiтень]
    $.5[Травень]
    $.6[Червень]
    $.7[Липень]
    $.8[Серпень]
    $.9[Вересень]
    $.10[Жовтень]
    $.11[Листопад]
    $.12[Грудень]
  ]
  $.weekday[
    $.0[Недiля]
    $.1[Понедiлок]
    $.2[Вiвторок]
    $.3[Середа]
    $.4[Четвер]
    $.5[П'ятниця]
    $.6[Субота]
    $.7[Недiля]
  ]
]
$us-low-locale[
  $.month[
    $.1[сiч]
    $.2[лют]
    $.3[бер]
    $.4[квi]
    $.5[тра]
    $.6[чер]
    $.7[лип]
    $.8[сер]
    $.9[вер]
    $.10[жов]
    $.11[лис]
    $.12[гру]
  ]
  $.weekday[
    $.0[нед]
    $.1[пон]
    $.2[вiв]
    $.3[сер]
    $.4[чет]
    $.5[п'я]
    $.6[суб]
    $.7[нед]
  ]
]
$ui-low-locale[
  $.month[
    $.1[сiчень]
    $.2[лютий]
    $.3[березень]
    $.4[квiтень]
    $.5[травень]
    $.6[червень]
    $.7[липень]
    $.8[серпень]
    $.9[вересень]
    $.10[жовтень]
    $.11[листопад]
    $.12[грудень]
  ]
  $.weekday[
    $.0[недiля]
    $.1[понедiлок]
    $.2[вiвторок]
    $.3[середа]
    $.4[четвер]
    $.5[п'ятниця]
    $.6[субота]
    $.7[недiля]
  ]
]

$tz[
  $.CST[CST6CDT]
  $.EST[EST5EDT]
  $.GMT[GMT0BST]
  $.MET[MET-1DST]
  $.MED[MET-2DST]
  $.MSK[MSK-3MSD]
  $.MSD[MSD-4MSK]
  $.MST[MST7MDT]
  $.PST[PST8PDT]
}]

$max_day[
  $.1(31)
  $.2(29)
  $.3(31)
  $.4(30)
  $.5(31)
  $.6(30)
  $.7(31)
  $.8(31)
  $.9(30)
  $.10(31)
  $.11(30)
  $.12(31)
]

$default[$ri-locale]

$yy[]
$mm[]
$dd[]

$tmp[^self.setLocale[$default]]

@create[uDate;uDefault]
## create new date object from string/date
## accept sql-like strings (%Y[-%m[-%d[ %H:%M[:%S]]]])
^if(def $uDate){
  ^if($uDate is "date"){
    $result[^date::create($uDate)]
  }{
    ^if($uDate is "string" && ^uDate.length[] >= 4){
      ^try{
        $result[^date::create[$uDate]]
      }{
        $exception.handled(1)
      }
    }
  }
}{
  ^if(def $uDefault){
    $result[^self.create[$uDefault]]
  }
}
^if(!def $result){
  ^throw[$sClassName;create;Can't create date.]
}

@format[sFormat;uDate;hLocale][dtDate]
## Print date using format string
## example: ^dtf:format[%Y-%m-%d], ^dtf:format[%d.%m.%Y;$date], ^dtf:format[%d %h %Y;$date;$dtf:rr-locale]
$dtDate[^self.create[$uDate;^date::now[]]]

^if(!def $hLocale){$hLocale[$self.locale]}

$result[^sFormat.match[%(.)][g]{^switch[$match.1]{
  ^case[%]{%}
  ^case[n]{^#0A}
  ^case[t]{^#09}

  ^case[e]{$dtDate.day}
  ^case[d]{^dtDate.day.format[%02d]}

  ^case[c]{$dtDate.month}
  ^case[m]{^dtDate.month.format[%02d]}
  ^case[h;B]{$hLocale.month.[$dtDate.month]}
  ^case[b]{^hLocale.month.[$dtDate.month].left(3)}

  ^case[Y]{$dtDate.year}
  ^case[y]{^eval($dtDate.year % 100)[%02d]}
  ^case[j]{$dtDate.yearday}

  ^case[w]{$dtDate.weekday}
  ^case[A]{$hLocale.weekday.[$dtDate.weekday]}
  ^case[a]{^hLocale.weekday.[$dtDate.weekday].left(3)}

  ^case[D]{^dtDate.month.format[%02d]/^dtDate.day.format[%02d]/$dtDate.year}
  ^case[F]{$dtDate.year/^dtDate.month.format[%02d]/^dtDate.day.format[%02d]}

  ^case[H]{^dtDate.hour.format[%02d]}
  ^case[k]{$dtDate.hour}
  ^case[i;M]{^dtDate.minute.format[%02d]}
  ^case[S]{^dtDate.second.format[%02d]}
  ^case[s]{^dtDate.unix-timestamp[]}

  ^case[T]{^dtDate.hour.format[%02d]:^dtDate.minute.format[%02d]:^dtDate.second.format[%02d]}
  ^case[R]{^dtDate.hour.format[%02d]:^dtDate.minute.format[%02d]}
  ^case[r]{^if($dtDate.hour > 0 && $dtDate.hour < 13){$dtDate.hour}{^if($dtDate.hour < 1)($dtDate.hour + 12)($dtDate.hour - 12)}:^dtDate.minute.format[%02d]:^dtDate.second.format[%02d]}
  ^case[p]{^if($dtDate.hour > 11){PM}{AM}}
  ^case[P]{^if($dtDate.hour > 11){pm}{am}}
  
  ^case[_]{$es-locale.weekday.[$dtDate.weekday], ^dtDate.day.format[%02d] $es-locale.month.[$dtDate.month] $dtDate.year ^dtDate.hour.format[%02d]:^dtDate.minute.format[%02d]:^dtDate.second.format[%02d]}
}}]

@last-day[uDate]
## Return date of last day for month of specified[current] day
$result[^self.create[$uDate;^date::now[]]]
^result.roll[month](+1)
^result.roll[day](-$result.day)

@setLocale[hLocale]
## Set new locale returning old one
$result[$self.locale]
^if(def $hLocale){
  $self.locale[$hLocale]
  ^self._init[]
}
@resetLocale[]
## Reset locale to default
$self.locale[$default]

@from822[sDate][sMethodName;tPart;hDate;sMonthName;sKey;sValue;dtDate;iDiff]
## Create date object from date string in RFC822/2822 format ( http://www.faqs.org/rfcs/rfc2822.html, http://www.faqs.org/rfcs/rfc822.html )
## WARNING: still under construction
$sMethodName[from822]
^try{
  ^if(!def $sDate){
    ^throw[$sClassName;$sMethodName;Input date is empty]
  }
  $tPart[^sDate.match[(?:([a-z]{3}),\s+)?(\d{1,2})\s+([a-z]{3})\s+(\d{4}|\d{2})\s+(\d{2}):(\d{2})(?::(\d{2}))?\s+(\w{3,5})?(?:\s*([-+]?\d{1,2})(\d{2})?)?][i]]
  ^if($tPart){
    $hDate[
      $.weekday_name[$tPart.1]
      $.day($tPart.2)
      $.month(0)
      $.month_name[$tPart.3]
      $.year($tPart.4)
      $.hour($tPart.5)
      $.min($tPart.6)
      $.sec(^tPart.7.int(0))
      $.tz[$tPart.8]
      $.offset_hour(^tPart.9.int(0))
      $.offset_min(^tPart.10.int(0))
    ]

    ^rem{ *** fix 2-digit year *** }
    ^if($hDate.year < 100){
      ^hDate.year.inc(2000)
    }

    ^rem{ *** check month abbr *** }
    $sMonthName[^hDate.month_name.lower[]]
    ^es-locale.month.foreach[sKey;sValue]{
      ^if(^sValue.lower[] eq $sMonthName){
        $hDate.month($sKey)
      }
    }
    ^if(!$hDate.month){
      ^throw[$sClassName;$sMethodName;Unknown month '$hDate.month_name']
    }

    ^if(def $hDate.offset_hour && !def $hDate.tz){
      $hDate.tz[GMT]
    }
    
    $result[^date::create($hDate.year;$hDate.month;$hDate.day;$hDate.hour;$hDate.min;$hDate.sec)]

    ^rem{ *** check weekday abbr *** }
    ^if(
      def $hDate.weekday_name
      && ^hDate.weekday_name.lower[] ne ^es-locale.weekday.[$result.weekday].lower[]
    ){
      ^throw[$sClassName;$sMethodName;Incorrect day of week '$hDate.weekday_name' (must be '$es-locale.weekday.[$result.weekday]')]
    }
    
    ^rem{ *** roll time to timezone *** }
    ^if(def $tz.[$hDate.tz]){
      $dtDate[^date::create($result)]
      ^dtDate.roll[TZ;$tz.[$hDate.tz]]
      $iDiff(^date::create($dtDate.year;$dtDate.month;$dtDate.day;$dtDate.hour;$dtDate.minute;$dtDate.second) - $result)
      $result[^date::create($dtDate - $iDiff)]
    }
    
    ^rem{ *** apply timezone offset *** }
    ^if($hDate.offset_hour || $hDate.offset_min){
      $result[^date::create($result - ($hDate.offset_hour + $hDate.offset_min / 60) / 24)]
    }

    ^rem{ *** apply DST offset *** }
    ^if($result.daylightsaving){
      $result[^date::create($result + $result.daylightsaving / 24)]
    }
  }{
    ^throw[$sClassName;$sMethodName;Wrong RFC2822 date format]
  }
}{
  ^if($exception.type ne $sClassName){
    $exception.handled(1)
    ^throw[$sClassName;$sMethodName;Can't create date^if($hDate){ (${hDate.year}-${hDate.month}-$hDate.day ${hDate.hour}:${hDate.min}:${hDate.sec})}. ${exception.comment}.]
  }
}

@to822[uDate;sTZ][dtDate]
## Print specified date in RFC822/2822 format
## WARNING: still under construction
$dtDate[^self.create[$uDate]]
$dtDate[^date::create($dtDate-^dtDate.daylightsaving.int(0)/24)]
^if(!def $sTZ){$sTZ[GMT]}
^if(def $tz.$sTZ){
  ^dtDate.roll[TZ;$tz.$sTZ]
}
$result[^self.format[%_ $sTZ;^date::create($dtDate.year;$dtDate.month;$dtDate.day;$dtDate.hour;$dtDate.minute;$dtDate.second)]]

@setExpireHeaders[uDate][result;dtDate]
## Set response headers Last-Modified/Expires for prevent caching by browsers/proxies
^if(def $uDate){
  $dtDate[^self.create[$uDate]]
}
^if($dtDate && $dtDate < ^date::now(-7)){
  $response:Last-Modified[$dtDate]
}{
  $response:expires[Fri, 23 Mar 2001 09:32:23 GMT]
  $response:cache-control[no-store, no-cache, must-revalidate, proxy-revalidate]
  $response:pragma[no-cache]
}
$result[]

@_months[locale;is_lowercase][i]
## Create and return months table
^if(!def $locale){$locale[$self.locale]}
$result[^table::create{number name
^for[i](1;12){$i  ^if($is_lowercase){^locale.month.$i.lower[]}{$locale.month.$i}}[^#0A]}]

@_init[][i;_now]
## Create tables for day/month/year based on current locale
$_now[^date::now[]]

$yy[^table::create{number name
^for[i]($_now.year - 5;$_now.year + 5){$i $i}[^#0A]}]

$dd[^table::create{number name
^for[i](1;31){$i  ^i.format{%02d}}[^#0A]}]

$mm[^self._months[]]
$mm-r[^self._months[$rr-locale]]



# Backward for a while

@parse[date]
$result[^self.create[$date]]

@last-modifyed[date]
$result[^self.to822[$date]]


